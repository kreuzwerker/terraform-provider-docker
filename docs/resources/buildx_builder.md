---
page_title: "docker_buildx_builder Resource - terraform-provider-docker"
subcategory: ""
description: |-
  Manages a Docker Buildx builder instance. This resource allows you to create a  buildx builder with various configurations such as driver, nodes, and platform settings. Please see https://github.com/docker/buildx/blob/master/docs/reference/buildx_create.md for more documentation
---

# docker_buildx_builder (Resource)

Manages a Docker Buildx builder instance. This resource allows you to create a  buildx builder with various configurations such as driver, nodes, and platform settings. Please see https://github.com/docker/buildx/blob/master/docs/reference/buildx_create.md for more documentation

## CI/CD Auto-Recreation Feature

The `auto_recreate` attribute allows builders to be automatically recreated if they exist in Terraform state but are missing from Docker. This is particularly useful in CI/CD environments where Docker builders may be cleaned up between runs.

## Example Usage

### Basic Builder

```terraform
resource "docker_buildx_builder" "example" {
  name          = "example-builder"
  driver        = "docker-container"
  bootstrap     = true
  auto_recreate = false # Default: will fail if builder is missing from Docker

  docker_container {
    image        = "moby/buildkit:latest"
    default_load = true
  }

  platform = ["linux/amd64", "linux/arm64"]
}
```

### Auto-Recreation Enabled (CI/CD)

```terraform
# Example showing auto-recreation enabled for CI/CD environments
resource "docker_buildx_builder" "ci_builder" {
  name          = "ci-builder"
  driver        = "docker-container"
  bootstrap     = true
  auto_recreate = true # Automatically recreate if missing from Docker

  docker_container {
    image        = "moby/buildkit:latest"
    default_load = true
  }

  platform = ["linux/amd64", "linux/arm64"]
}

# Use the builder in a docker_image resource
resource "docker_image" "app" {
  name = "my-app:latest"

  build {
    context    = "."
    dockerfile = "Dockerfile"
    platform   = "linux/amd64,linux/arm64"

    # This builder will be auto-recreated if missing from Docker
    builder = "ci-builder"
  }

  depends_on = [docker_buildx_builder.ci_builder]
}
```

### Mixed Usage (Different Environments)

```terraform
# Example showing different auto_recreate settings for different environments
resource "docker_buildx_builder" "development" {
  name          = "dev-builder"
  driver        = "docker-container"
  bootstrap     = true
  auto_recreate = true # Enable for development - convenience

  docker_container {
    image = "moby/buildkit:latest"
  }

  platform = ["linux/amd64"]
}

resource "docker_buildx_builder" "production" {
  name          = "prod-builder"
  driver        = "docker-container"
  bootstrap     = true
  auto_recreate = false # Disable for production - explicit control

  docker_container {
    image        = "moby/buildkit:v0.22.0" # Pinned version for production
    default_load = false
  }

  platform = ["linux/amd64", "linux/arm64"]
}

resource "docker_buildx_builder" "k8s_builder" {
  name          = "k8s-builder"
  driver        = "kubernetes"
  bootstrap     = true
  auto_recreate = true # Enable for k8s environments

  kubernetes {
    namespace = "buildkit"
    image     = "moby/buildkit:latest"
    replicas  = 2

    requests {
      cpu    = "100m"
      memory = "256Mi"
    }

    limits {
      cpu    = "500m"
      memory = "512Mi"
    }
  }

  platform = ["linux/amd64", "linux/arm64"]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `append` (Boolean) Append a node to builder instead of changing it
- `auto_recreate` (Boolean) Automatically recreate this builder if it exists in Terraform state but missing in Docker. Defaults to `false`
- `bootstrap` (Boolean) Automatically boot the builder after creation. Defaults to `false`
- `buildkit_config` (String) BuildKit daemon config file
- `buildkit_flags` (String) BuildKit flags to set for the builder.
- `docker_container` (Block List, Max: 1) Configuration block for the Docker-Container driver. (see [below for nested schema](#nestedblock--docker_container))
- `driver` (String) The driver to use for the Buildx builder (e.g., docker-container, kubernetes).
- `driver_options` (Map of String) Additional options for the Buildx driver in the form of `key=value,...`. These options are driver-specific.
- `endpoint` (String) The endpoint or context to use for the Buildx builder, where context is the name of a context from docker context ls and endpoint is the address for Docker socket (eg. DOCKER_HOST value). By default, the current Docker configuration is used for determining the context/endpoint value.
- `kubernetes` (Block List, Max: 1) Configuration block for the Kubernetes driver. (see [below for nested schema](#nestedblock--kubernetes))
- `name` (String) The name of the Buildx builder. IF not specified, a random name will be generated.
- `node` (String) Create/modify node with given name
- `platform` (List of String) Fixed platforms for current node
- `remote` (Block List, Max: 1) Configuration block for the Remote driver. (see [below for nested schema](#nestedblock--remote))
- `use` (Boolean) Set the current builder instance as the default for the current context.

### Read-Only

- `id` (String) The ID of this resource.

<a id="nestedblock--docker_container"></a>
### Nested Schema for `docker_container`

Optional:

- `cgroup_parent` (String) Sets the cgroup parent of the container if Docker is using the "cgroupfs" driver.
- `cpu_period` (String) Sets the CPU CFS scheduler period for the container.
- `cpu_quota` (String) Imposes a CPU CFS quota on the container.
- `cpu_shares` (String) Configures CPU shares (relative weight) of the container.
- `cpuset_cpus` (String) Limits the set of CPU cores the container can use.
- `cpuset_mems` (String) Limits the set of CPU memory nodes the container can use.
- `default_load` (Boolean) Automatically load images to the Docker Engine image store. Defaults to `false`
- `env` (Map of String) Sets environment variables in the container.
- `image` (String) Sets the BuildKit image to use for the container.
- `memory` (String) Sets the amount of memory the container can use.
- `memory_swap` (String) Sets the memory swap limit for the container.
- `network` (String) Sets the network mode for the container.
- `restart_policy` (String) Sets the container's restart policy.


<a id="nestedblock--kubernetes"></a>
### Nested Schema for `kubernetes`

Optional:

- `annotations` (String) Sets additional annotations on the deployments and pods.
- `default_load` (Boolean) Automatically load images to the Docker Engine image store. Defaults to `false`
- `image` (String) Sets the image to use for running BuildKit.
- `labels` (String) Sets additional labels on the deployments and pods.
- `limits` (Block List, Max: 1) Resource limits for CPU, memory, and ephemeral storage. (see [below for nested schema](#nestedblock--kubernetes--limits))
- `loadbalance` (String) Load-balancing strategy (sticky or random).
- `namespace` (String) Sets the Kubernetes namespace.
- `nodeselector` (String) Sets the pod's nodeSelector label(s).
- `qemu` (Block List, Max: 1) QEMU emulation configuration. (see [below for nested schema](#nestedblock--kubernetes--qemu))
- `replicas` (Number) Sets the number of Pod replicas to create.
- `requests` (Block List, Max: 1) Resource requests for CPU, memory, and ephemeral storage. (see [below for nested schema](#nestedblock--kubernetes--requests))
- `rootless` (Boolean) Run the container as a non-root user.
- `schedulername` (String) Sets the scheduler responsible for scheduling the pod.
- `serviceaccount` (String) Sets the pod's serviceAccountName.
- `timeout` (String) Set the timeout limit for pod provisioning.
- `tolerations` (String) Configures the pod's taint toleration.

<a id="nestedblock--kubernetes--limits"></a>
### Nested Schema for `kubernetes.limits`

Optional:

- `cpu` (String) CPU limit for the Kubernetes pod.
- `ephemeral_storage` (String) Ephemeral storage limit for the Kubernetes pod.
- `memory` (String) Memory limit for the Kubernetes pod.


<a id="nestedblock--kubernetes--qemu"></a>
### Nested Schema for `kubernetes.qemu`

Optional:

- `image` (String) Sets the QEMU emulation image.
- `install` (Boolean) Install QEMU emulation for multi-platform support.


<a id="nestedblock--kubernetes--requests"></a>
### Nested Schema for `kubernetes.requests`

Optional:

- `cpu` (String) CPU limit for the Kubernetes pod.
- `ephemeral_storage` (String) Ephemeral storage limit for the Kubernetes pod.
- `memory` (String) Memory limit for the Kubernetes pod.



<a id="nestedblock--remote"></a>
### Nested Schema for `remote`

Optional:

- `cacert` (String) Absolute path to the TLS certificate authority used for validation.
- `cert` (String) Absolute path to the TLS client certificate to present to buildkitd.
- `default_load` (Boolean) Automatically load images to the Docker Engine image store. Defaults to `false`
- `key` (String) Sets the TLS client key.
- `servername` (String) TLS server name used in requests.

## Import

Import is supported using the following syntax:

```shell
#!/bin/bash

# Import an existing Docker Buildx builder
terraform import docker_buildx_builder.example my-existing-builder

# Note: After import, you may want to enable auto_recreate for the imported builder
# to ensure it gets recreated if missing from Docker in the future
```

